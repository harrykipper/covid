;; ===========================================================================
;;       Model configuration  --- TRANSITION PROBABILITIES and TIMIGS
;;
;;    Change the following values to implement a different disease course
;;============================================================================

to-report average-recovery-time [agent-age]
  ;; Agents recover, on average, after these many days since being infected
  report (ifelse-value
    agent-age < 40 [8]    ;; agents < 40 y.o recover in 8 days on average
    agent-age < 50 [12]   ;; agents between 40 and 50 recover in 12 days on avg.
    agent-age < 60 [15]   ;; agents between 50 and 60
    [20]                  ;; agents above 60
    )
end

;; The three parameters below interact.
;; The probability of someone dying of covid is:
;; probability-of-showing-syptoms * probability-of-worsening * probability-of-dying
;; For someone between 30 and 40 this is currently 0.45 * 0.02 * 0.05 = 0.00045 or 0.045%

to-report probability-of-showing-symptoms [agent-age]
  ;; When the incubation period ('incubation-days' in main interface) is over
  ;; the agent has the following probability of developing the symptoms of COVID-19.
  ;; If the person is showing symptoms, it'll take 7 more days to recover.
  report (ifelse-value
    agent-age < 15 [3 * b]      ;; Children rarely show any symptoms
    agent-age < 40 [40 * b]     ;; Agents between 30 and 40
    agent-age <= 50 [50 * b]    ;; agents between 40 and 50
    agent-age < 60 [60 * b]     ;; agents between 50 and 60
    [70 * b]                    ;; agents above 60
  )
end

to-report probability-of-worsening [agent-age]
  ;; After 10 days with symptoms the agent has the following probability of
  ;; worsening and needing hospital care.
  report (ifelse-value
    agent-age < 15 [3 / b]         ;; 
    agent-age < 40 [4 / b]         ;; agents below age 40 who display symptoms have a 2% chance of ending up in hospital
    agent-age <= 50 [8 / b]         ;; agents between 40 and 50 ....
    agent-age < 60 [10 / b]        ;; agents between 50 and 60
    [15 / b]                       ;; over 60
  )
end

to-report probability-of-dying [agent-age]
  ;; After 10 days in the hospital the agent has the following probability of dying
  report (ifelse-value
    agent-age < 40 [5]
    agent-age < 50 [10]
    agent-age < 60 [18]
    [25]
  )
end

to assign-tendency 
  set isolation-tendency random-normal average-isolation-tendency (average-isolation-tendency / 4)
  if isolation-tendency > 100 [ set isolation-tendency 100 ]
  if isolation-tendency < 0 [ set isolation-tendency 0 ]
  set recovery-time round (random-normal (average-recovery-time age) 1)
  set prob-symptoms ((probability-of-showing-symptoms age) * gender-discount)
  set symptom-time round random-gamma incubation-days 0.95   ;5.8 0.95
  if recovery-time < symptom-time [set recovery-time symptom-time + 1]
  
  ;; The moment someone deteriorates: minimum bw (7 to 12) and recovery time.
  set worsening-time min (list (7 + random 5) recovery-time)

  set infectivity-time max (list 1 (symptom-time - random 3))  ;; Apparently people are infectious up to 3 (wasn't it one?) days before showing symptoms
  
  set testing-urgency 1 + random 3 

 
end